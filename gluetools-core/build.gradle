apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
version = '1.0'

jar {
    manifest {
        attributes 'Implementation-Title': 'GLUE tools',
                   'Implementation-Version': version
    }
}

task jarAll(type: Jar) {
	manifest {
        attributes 'Implementation-Title': 'GLUE tools',  
        	       'Implementation-Version': version,
                   'Main-Class': 'uk.ac.gla.cvr.gluetools.core.console.Console'

    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

repositories {
    mavenCentral()
	// this is for the vpp dependency of the cayenne tools. 
    maven {
    	url 'http://repo.spring.io/libs-release-remote/'
    }
    // required for j-text-utils
    // maven {
    //	url 'http://d-maven.googlecode.com/svn/trunk/repo'
    // }
}


task checkCayenneTools << {
    ant.taskdef(name:'cgen', 
    			classname:'org.apache.cayenne.tools.CayenneGeneratorTask', 
    			classpath: configurations.cgen.asPath)
}


configurations {
    cgen
}


// uses cayenne to generate the datamodel classes.
task datamodel(dependsOn: 'checkCayenneTools') << {
	String destDir = 'src/main/java'
	String superPkg = 'uk.ac.gla.cvr.gluetools.core.datamodel.auto'
	String mapFile1 = 'src/main/resources/gluecore-map.map.xml'
	ant.cgen(map: mapFile1, destDir: destDir, usepkgpath: true, superpkg: superPkg)
	String mapFile2 = 'src/main/resources/glueproject-map.map.xml'
	ant.cgen(map: mapFile2, destDir: destDir, usepkgpath: true, superpkg: superPkg)
	String mapFile3 = 'src/main/resources/gluemeta-map.map.xml'
	ant.cgen(map: mapFile3, destDir: destDir, usepkgpath: true, superpkg: superPkg)
}

dependencies {
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.4.1'
    compile group: 'org.apache.cayenne', name: 'cayenne-server', version:'3.1'
    compile group: 'org.apache.cayenne', name: 'cayenne-project', version:'3.1'
    compile group: 'org.biojava', name: 'biojava-core', version: '4.0.0'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.20'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.35'
    compile group: 'org.apache.derby', name: 'derby', version: '10.5.3.0_1'
    compile group: 'org.eclipse.persistence', name: 'org.eclipse.persistence.jpa', version: '2.6.0'
    compile group: 'org.eclipse.persistence', name: 'javax.persistence', version: '2.1.0'
    compile group: 'jline', name: 'jline', version: '2.12.1'
    compile group: 'com.brsanthu', name: 'data-exporter', version: '1.0.4'
    // compile group: 'dnl.utils', name: 'j-text-utils', version: '0.3.3'
    compile group: 'com.offbytwo', name:'docopt', version:'0.6.0.20150202'
    
    testCompile group: 'junit', name: 'junit', version: '4.+'
    cgen group: 'org.apache.cayenne', name: 'cayenne-tools', version: '3.1'
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}
